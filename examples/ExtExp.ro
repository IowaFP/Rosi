import Ro.Base, Ro.Xr, Data.Functor, Data.List, Data.Nat, Data.Tuple

-------------------------------------------------------------------------------
-- Types

type BoolF : R[* -> *]
type BoolF = { 'BConst := (\t. Bool)
             , 'If := (\t. Triple t t t) }

type ArithF : R[* -> *]
type ArithF = { 'IConst := (\t. Nat)
              , 'Plus := (\t. Pair t t) }

type LamF : R[* -> *]
type LamF = { 'Var := Const Nat
            , 'Lam := Id
            , 'App := (\t. Pair t t) }

type RecF : R[* -> *]
type RecF = { 'Fix := (\t. t) }

-------------------------------------------------------------------------------
-- Functors

functorId : Functor Id
functorId = id

functorConst : forall t. Functor (Const t)
functorConst = \f. id

functorTwo : Functor (\t. Pair t t)
functorTwo = \f x. pair (f (sel x #'1)) (f (sel x #'2))

functorThree : Functor (\t. Triple t t t)
functorThree = \f x. triple (f (sel x #'1)) (f (sel x #'2)) (f (sel x #'3))

functorBool : Pi (Functor BoolF)
functorBool = (#'BConst := functorConst) ++ (#'If := functorThree)

functorArith : Pi (Functor ArithF)
functorArith = (#'IConst := functorConst) ++ (#'Plus := functorTwo)

functorLam : Pi (Functor LamF)
functorLam = (#'Var := functorConst) ++ (#'Lam := functorId)
           ++ (#'App := functorTwo)

functorRec : Pi (Functor RecF)
functorRec = #'Fix := functorId

-------------------------------------------------------------------------------
-- Shorthand

type LBF : R [* -> *]
type LBF = { 'Var := (\t. Nat), 'Lam := Id, 'App := (\t. Pair t t),
             'BConst := (\t. Bool), 'If := (\t. Triple t t t) }

type AllF : R [* -> *]
type AllF = { 'Var := (\t. Nat), 'Lam := Id, 'App := (\t. Pair t t),
              'BConst := (\t. Bool), 'If := (\t. Triple t t t),
              'IConst := (\t. Nat), 'Plus := (\t. Pair t t),
              'Fix := (\t. t) }

functorLBF : Pi (Functor LBF)
functorLBF = functorBool ++ functorLam

functorAll : Pi (Functor AllF)
functorAll = functorArith
          ++ functorLBF
          ++ functorRec

-------------------------------------------------------------------------------
-- Some expressions

xpId : Mu (Sigma AllF)
xpId = rcon #'Lam (rcon #'Var zero)

xpConst : Mu (Sigma AllF)
xpConst = rcon #'Lam (rcon #'Lam (rcon #'Var one))

xpNot : Mu (Sigma AllF)
xpNot = rcon #'Lam (rcon #'If (triple (rcon #'Var zero) (rcon #'BConst False) (rcon #'BConst True)))

-------------------------------------------------------------------------------
-- Depth

depthB : FAlg (Sigma BoolF) Nat    -- Sigma BoolF Nat -> Nat
depthB = case #'BConst (\u. one)
       | case #'If (\t. succ (max (sel t #'1) (max (sel t #'2) (sel t #'3))))

depthA : FAlg (Sigma ArithF) Nat
depthA = case #'IConst (\u. one)
       | case #'Plus (\t. succ (max (sel t #'1) (sel t #'2)))

depthL : FAlg (Sigma LamF) Nat
depthL = case #'Lam succ
       | case #'App (\t. succ (max (sel t #'1) (sel t #'2)))
       | case #'Var (\t. one)

depthR : FAlg (Sigma RecF) Nat
depthR = case #'Fix succ

depthLB : FAlg (Sigma LBF) Nat
depthLB = depthL | depthB

depthAll : FAlg (Sigma AllF) Nat
depthAll = depthLB
         | depthA
         | depthR

-- Examples

dpId : Nat
dpId = cata (fmapS [AllF] functorAll) depthAll xpId

dpConst : Nat
dpConst = cata (fmapS [AllF] functorAll) depthAll xpConst

dpNot : Nat
dpNot = cata (fmapS [AllF] functorAll) depthAll xpNot

-- Depth as a bounded algebra

type RConst : * -> R[* -> *] -> *  -- kind polymorphism still not a thing...
type RConst = \x y. x

depthBb : BAlg BoolF (RConst Nat)
depthBb = case #'BConst (\u rec. one)
        | case #'If (\t rec. succ (max (rec (sel t #'1)) (max (rec (sel t #'2)) (rec (sel t #'3)))))

depthAb : BAlg ArithF (RConst Nat)
depthAb = case #'IConst (\u rec. one)
        | case #'Plus (\t rec. succ (max (rec (sel t #'1)) (rec (sel t #'2))))

depthLb : BAlg LamF (RConst Nat)
depthLb = case #'Lam (\e rec. succ (rec e))
        | case #'App (\t rec. succ (max (rec (sel t #'1)) (rec (sel t #'2))))
        | case #'Var (\t rec. one)

depthRb : BAlg RecF (RConst Nat)
depthRb = case #'Fix (\e rec. succ (rec e))

-- Okay, the type annotations here are getting out of hand again... time to
-- figure out what's going wrong in type inference.

type ABF : R[* -> *]
type ABF = { 'BConst := (\t. Bool), 'If := (\t. Triple t t t)
           , 'IConst := (\t. Nat), 'Plus := (\t. Pair t t) }

type LRF : R[* -> *]
type LRF = { 'Var := Const Nat, 'Lam := Id, 'App := (\t. Pair t t)
           , 'Fix := (\t. t) }

depthAllb : BAlg AllF (RConst Nat)
depthAllb = (depthBb | depthAb) | (depthLb | depthRb)

-- Examples (again)

depthb : Mu (Sigma AllF) -> Nat
depthb = histo depthAllb

dpId0 : Nat
dpId0 = depthb xpId

dpConst0 : Nat
dpConst0 = depthb xpConst

dpNot0 : Nat
dpNot0 = depthb xpNot

-------------------------------------------------------------------------------
-- Evaluation
--
-- The idea is that we'd write functions like:
--
--     evalA = case #'IConst (\n. n)
--           | case #'Plus (\p. add (evalA (sel p #'1)) (evalA (sel p #'2)))
--
-- and then put them together to make the eval function for the whole type. The
-- challenges are:
--
--   1. Each sublanguage will have its own evaluation results: natural numbers,
--      Booleans, or whatever.
--   2. Therefore, the results of evaluation may not actually be the right type.
--   3. The results of the function sublanguage *include* the original language,
--      'cause closures.
--
-- Put those together, and we get something more like:


as : forall l f z. { l := f } < z, { 'Err := (\w. Unit) } < z =>
     #l -> Mu (Sigma z) -> (f (Mu (Sigma z)) -> Mu (Sigma z)) -> Mu (Sigma z)
as = \l w f.
      match (out w)
      ( case l f
      | const (rcon #'Err tt))

type Env : R[* -> *] -> *
type Env = \z. List (Mu (Sigma z))

evalA : forall valr.
        { 'Nat := (\expr. \val. Nat), 'Err := (\expr. \val. Unit) } < valr =>
          BAlg ArithF (\expr. Env (valr expr) -> Mu (Sigma (valr expr)))
evalA = \exp rec env.
           match exp
           ( case #'IConst (rcon #'Nat)
           | case #'Plus (\p.
               as #'Nat (rec (sel p #'1) env) (\i.
               as #'Nat (rec (sel p #'2) env) (\j.
                 rcon #'Nat (add i j))))
           )

evalB : forall valr.
        { 'Bool := (\expr. \val. Bool), 'Err := (\expr. \val. Unit) } < valr =>
        BAlg BoolF (\expr. Env (valr expr) -> Mu (Sigma (valr expr)))
evalB = \exp rec env.
           match exp
           ( case #'BConst (rcon #'Bool)
           | case #'If (\t.
               as #'Bool (rec (sel t #'1) env)
               ( case #'True (const (rec (sel t #'2) env))
               | case #'False (const (rec (sel t #'3) env))
               ))
           )

evalF : forall valr.
        { 'Clos := (\expr. \val. Pair (List val) (Mu (Sigma expr)))
        , 'Err := (\expr. \val. Unit) } < valr =>
        BAlg LamF
             (\expr. Env (valr expr) -> Mu (Sigma (valr expr)))
evalF = \exp rec env.
           match exp
           ( case #'Var (o (maybe (rcon #'Err tt) id) (nth env))
           | case #'Lam (\e. rcon #'Clos (pair env e))
           | case #'App (\p.
               as #'Clos (rec (fst p) env) (\clos.
                 (rec (snd clos)) (cons (rec (snd p) env) (fst clos))))
           )

evalAB : forall valr.
         { 'Nat := (\expr. \val. Nat), 'Bool := (\expr. \val. Bool), 'Err := (\expr. \val. Unit) } < valr =>
         BAlg ABF (\expr. Env (valr expr) -> Mu (Sigma (valr expr)))
evalAB = evalA | evalB

xpThree : Mu (Sigma (ArithF))
xpThree = rcon #'Plus (pair (rcon #'IConst one) (rcon #'IConst two))

evThree : Mu (Sigma { 'Nat := (\val. Nat), 'Err := (\val. Unit) })
evThree = histo evalA xpThree nil

xpThreee : Mu (Sigma (ABF))
xpThreee = rcon #'Plus (pair (rcon #'IConst one) (rcon #'IConst two))

evThreee : Mu (Sigma { 'Nat := (\val. Nat), 'Bool := (\val. Bool), 'Err := (\val. Unit) })
evThreee = histo evalAB xpThreee nil

type ABLF : R[* -> *]
type ABLF = { 'BConst := (\t. Bool), 'If := (\t. Triple t t t)
            , 'IConst := (\t. Nat), 'Plus := (\t. Pair t t)
            , 'Var := Const Nat, 'Lam := Id, 'App := (\t. Pair t t) }

evalABL : forall valr.
        { 'Nat := (\expr. \val. Nat), 'Bool := (\expr. \val. Bool)
        , 'Clos := (\expr. \val. Pair (List val) (Mu (Sigma expr)))
        , 'Err := (\expr. \val. Unit) } < valr =>
        BAlg ABLF (\expr. Env (valr expr) -> Mu (Sigma (valr expr)))
evalABL = evalAB | evalF

xpThreeee : Mu (Sigma ABLF)
xpThreeee = rcon #'App (pair (rcon #'Lam (rcon #'Var zero))
                             (rcon #'Plus (pair (rcon #'IConst one) (rcon #'IConst two))))

evThreeee : Mu (Sigma { 'Nat := (\val. Nat), 'Bool := (\val. Bool)
                      , 'Clos := (\val. Pair (List val) (Mu (Sigma ABLF)))
                      , 'Err := (\val. Unit) })
evThreeee = histo evalABL xpThreeee nil

xpTrue : Mu (Sigma ABLF)
xpTrue = rcon #'App (pair (rcon #'Lam (rcon #'Var zero))
                          (rcon #'BConst True))

evTrue : Mu (Sigma { 'Nat := (\val. Nat), 'Bool := (\val. Bool)
                   , 'Clos := (\val. Pair (List val) (Mu (Sigma ABLF)))
                   , 'Err := (\val. Unit) })
evTrue = histo evalABL xpTrue nil

xpIdd : Mu (Sigma ABLF)
xpIdd = rcon #'App (pair (rcon #'Lam (rcon #'Var zero))
                         (rcon #'Lam (rcon #'Var zero)))

evIdd : Mu (Sigma { 'Nat := (\val. Nat), 'Bool := (\val. Bool)
                  , 'Clos := (\val. Pair (List val) (Mu (Sigma ABLF)))
                  , 'Err := (\val. Unit) })
evIdd = histo evalABL xpIdd nil

xpBoom : Mu (Sigma ABLF)
xpBoom = rcon #'App (pair (rcon #'BConst True) (rcon #'BConst True))

evBoom : Mu (Sigma { 'Nat := (\val. Nat), 'Bool := (\val. Bool)
                   , 'Clos := (\val. Pair (List val) (Mu (Sigma ABLF)))
                   , 'Err := (\val. Unit) })
evBoom = histo evalABL xpBoom nil

-------------------------------------------------------------------------------
-- Desugaring
--
-- Next challenge is to implement desugaring. The key point here is to highlight
-- label-generic programming.
--
-- As an example, we'll implement Booleans as functions. We're relying on this
-- being an untyped language, so we don't have to account for polymorphism.

desugarB : forall z.
           LamF < z =>
           BAlg BoolF (\w. Mu (Sigma z))
desugarB = \exp rec.
            match exp
            (case #'BConst
               ( case #'True (\u. rcon #'Lam (rcon #'Lam (rcon #'Var one)))
               | case #'False (\u. rcon #'Lam (rcon #'Lam (rcon #'Var zero)))
               )
            | case #'If (\t. rcon #'App (pair (rcon #'App (pair (rec (fst t)) (rec (snd t)))) (rec (thd t))))
            )

desugar : forall y z.
          BoolF < y, y - BoolF < z, LamF < z =>
          Pi (Functor (y - BoolF)) -> BAlg y (\w. Mu (Sigma z))
desugar = \d. desugarB | (\exp rec. in (inj (fmapS d rec exp)))

xpNott : Mu (Sigma LBF)
xpNott = rcon #'Lam (rcon #'If (triple (rcon #'Var zero) (rcon #'BConst False) (rcon #'BConst True)))

desNott : Mu (Sigma LamF)
desNott = histo (desugar functorLam) xpNott